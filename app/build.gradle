apply plugin: 'com.android.application'

android {
    defaultConfig {
        applicationId = 'com.pcinpact'
        // SDK utilisé pour compiler l'application
        compileSdk = 36
        // minSdkVersion est la version minimum pour faire tourner l'application (5.0)
        minSdkVersion 21
        // targetSdkVersion est la version la plus élevée testée et fonctionnelle (pas besoin de faire tourner la rétro-compatibilité)
        targetSdkVersion 36
        // Enregistrer la date du build
        buildConfigField 'long', 'TIMESTAMP', System.currentTimeMillis() + 'L'
        // versionCode est utilisé par Google pour les MàJ, versionName est uniquement affiché à l'utilisateur
        versionCode = 291
        versionName = '2.9.1'
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    /**
     * VS bug dans les tests sinon
     * https://sites.google.com/a/android.com/tools/tech-docs/unit-testing-support#TOC-Method-...-not-mocked.-
     */
    testOptions {
        unitTests.returnDefaultValues = true
    }

    /**
     * Configuration de lint
     */
    lint {
        // https://github.com/bumptech/glide/issues/4940
        lintConfig = file("$rootDir/android-lint.xml")
        // Affichage sur stdout des erreurs
        textOutput = file('stdout')
        textReport = true
    }
    namespace = 'com.pcinpact'
    buildFeatures {
        buildConfig = true
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile).tap {
            configureEach {
                options.compilerArgs << '-Xlint:unchecked'
            }
        }
    }
}

/**
 * Dépendances de l'application
 */
dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.2'
    implementation 'org.jsoup:jsoup:1.21.1'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.github.bumptech.glide:annotations:4.16.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
    implementation 'com.squareup.okhttp3:okhttp:5.1.0'
    implementation 'com.github.andremion:counterfab:1.2.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

/**
 * Affichage des warnings java à la compilation
 */
allprojects {
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs << '-Xlint:deprecation'
        }
    }
}