apply plugin: 'com.android.application'

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.1"

    defaultConfig {
        applicationId "com.pcinpact"
        // minSdkVersion est la version minimum pour faire tourner l'application (5.0)
        minSdkVersion 21
        // targetSdkVersion est la version la plus élevée testée et fonctionnelle (pas besoin de faire tourner la rétro-compatibilité)
        targetSdkVersion 33
        // versionCode est utilisé par Google pour les MàJ, versionName est uniquement affiché à l'utilisateur
        versionCode 268
        versionName "2.6.8"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    /**
     * VS bug dans les tests sinon
     * https://sites.google.com/a/android.com/tools/tech-docs/unit-testing-support#TOC-Method-...-not-mocked.-
     */
    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    /**
     * Configuration de lint
     */
    lint {
        // Affichage sur stdout des erreurs
        textOutput file('stdout')
        textReport true
    }
    namespace 'com.pcinpact'
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked"
        }
    }
}

/**
 * Dépendances de l'application
 */
dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.preference:preference:1.2.0"
    implementation 'com.google.android.material:material:1.7.0'
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    implementation 'com.github.bumptech.glide:annotations:4.14.2'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.14.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.14.2'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.github.andremion:counterfab:1.2.2'
}

/**
 * Affichage des warnings java à la compilation
 */
allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }
}