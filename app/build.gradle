apply plugin: 'com.android.application'

android {
    namespace 'com.pcinpact'
    compileSdkVersion 33
    buildToolsVersion "33.0.1"

    defaultConfig {
        applicationId "com.pcinpact"
        // minSdkVersion est la version minimum pour faire tourner l'application (5.0)
        minSdkVersion 21
        // targetSdkVersion est la version la plus élevée testée et fonctionnelle (pas besoin de faire tourner la rétro-compatibilité)
        targetSdkVersion 33
        // versionCode est utilisé par Google pour les MàJ, versionName est uniquement affiché à l'utilisateur
        versionCode 268
        versionName "2.6.8"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    /**
     * Configuration de lint
     */
    lint {
        // https://github.com/bumptech/glide/issues/4940
        lintConfig = file("$rootDir/android-lint.xml")
        // Affichage sur stdout des erreurs
        textOutput file('stdout')
        textReport true
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked"
        }
    }
}

/**
 * Dépendances de l'application
 */
dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'org.jsoup:jsoup:1.15.4'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.preference:preference:1.2.0"
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    implementation 'com.github.bumptech.glide:annotations:4.15.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.15.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.15.1'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.github.andremion:counterfab:1.2.2'

    /*
     * Gestion des dépendances spécifiques Kotlin
     * https://youtrack.jetbrains.com/issue/KT-54136/Duplicated-classes-cause-build-failure-if-a-dependency-to-kotlin-stdlib-specified-in-an-android-project
     * https://stackoverflow.com/questions/75263047/duplicate-class-in-kotlin-android
     */
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

/**
 * Affichage des warnings java à la compilation
 */
allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }
}